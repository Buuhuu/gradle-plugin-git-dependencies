group = 'org.batcha.gradle.plugins'
description = 'The Git dependencies plugin provides integration of other Gradle project dependencies via git'

apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'release'

release {
  failOnSnapshotDependencies = true
  allowLocalModifications = false
  releaseDryRun = false
  scm = 'git'
}
version = release.projectVersion

apply plugin: 'signing'
apply plugin: 'github-pages'
apply plugin: 'jacoco'
apply plugin: 'sonar-runner'
apply from: rootProject.file('gradle/publishing.gradle')
apply from: rootProject.file('gradle/license.gradle')

buildscript {
  repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'http://maven.tmatesoft.com/content/repositories/releases/' }
    maven { url 'http://maven.tmatesoft.com/content/repositories/snapshots/' }
  }
  dependencies {
    classpath 'org.ajoberstar:gradle-git:0.6.3'
    classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.7.0'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    classpath 'au.com.ish.gradle:release:2.2.2'
  }
}

sonarRunner.sonarProperties {
  property 'sonar.language', 'grvy'
}

sourceCompatibility = '1.7'

jacoco.toolVersion = '0.7.0.201403182114'

repositories {
  mavenCentral()
}

def jgitVersion = '3.3.0.201403021825-r'

dependencies {
  compile gradleApi()  
  compile localGroovy()
  compile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
  testCompile group: 'junit', name: 'junit', version: '4.11'
  
}

githubPages {
  repoUri = 'git@github.com:bat-cha/gradle-plugin-git-dependencies.git'
  pages {
    from(javadoc.outputs.files) {
      into 'docs/javadoc'
    }
    from(groovydoc.outputs.files) {
      into 'docs/groovydoc'
    }
  }
}

task wrapper(type: Wrapper) { gradleVersion = '1.11' }

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
  classifier = 'javadoc'
  from javadoc.outputs.files
}

task groovydocJar(type: Jar, dependsOn:groovydoc) {
  classifier = 'groovydoc'
  from groovydoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
  archives groovydocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      name = 'Sonatype OSS'
      repository(url:'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
        authentication(userName:sonatypeUserName, password:sonatypePassword)
      }
      snapshotRepository(url:'https://oss.sonatype.org/content/repositories/snapshots') {
        authentication(userName:sonatypeUserName, password:sonatypePassword)
      }
  
      beforeDeployment { signing.signPom it }
      
      pom.project {
        name project.name
        description project.description
        url "https://github.com/bat-cha/gradle-plugin-git-dependencies"
        licenses {
          license {
            name "LGPL V3 License"
            url "https://raw.github.com/bat-cha/gradle-plugin-git-dependencies/master/lgpl-3.0.txt"
          }
        }
        developers {
          developer {
            id "bat-cha"
            name "Baptiste Chatrain"
            email "baptiste.chatrain@gmail.com"
          }
        }
        scm {
          connection "scm:git:git://github.com/bat-cha/gradle-plugin-git-dependencies.git"
          developerConnection "scm:git:git@github.com:bat-cha/gradle-plugin-git-dependencies.git"
          url "https://github.com/bat-cha/gradle-plugin-git-dependencies"
        }
      }
    }
  }
}
